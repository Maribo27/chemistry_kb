scp_program -> proc_search_of_object_structure
(*
	-> rrel_params: .proc_search_of_object_structure_params 
	(*
		-> rrel_1: rrel_in: rrel_fixed: _ans;;
		-> rrel_2: rrel_in: rrel_fixed: _obj;;
	*);;

	-> rrel_operators: .proc_search_of_object_structure_operatorset 
	(*
		-> rrel_init: .proc_search_of_object_structure_operator0
		(*
			<- printEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _ans;;

			=> nrel_goto: .proc_search_of_object_structure_operator1;;
		*);; 




		-> .proc_search_of_object_structure_operator1 
		(*
			<- searchSetStr5;;
		
			-> rrel_1: rrel_fixed: rrel_scp_var: _obj;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _aarc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _el;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _aarc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_object_structure;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _set1;;

			=> nrel_then: .proc_search_of_object_structure_operator2;;
			=> nrel_else: .proc_search_of_object_structure_operatorreturn;;
		*);;

		//поиск по множеству элементов, если оно не пустое, то переходим к следующему пункту, иначе выводим ответ и выходим из программы
		-> .proc_search_of_object_structure_operator2
		(*
			<- searchElStr3;;	

			-> rrel_1: rrel_fixed: rrel_scp_var: _set1;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _aarc3;;
			-> rrel_3: rrel_assign: rrel_scp_var: _elem;;

			=> nrel_then: .proc_search_of_object_structure_operator3;;
			=> nrel_else: .proc_search_of_object_structure_operatorreturn;;
		*);;

		//удаление рассматриваемого элемента из множества
		-> .proc_search_of_object_structure_operator3
		(*
			<- eraseEl;;
		
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _aarc3;;

			=> nrel_goto: .proc_search_of_object_structure_operator4;;
		*);;

		-> .proc_search_of_object_structure_operator4 
		(*
			<- call;;
			
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_search_of_object_structure;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... 
			(*
				-> rrel_1: rrel_fixed: rrel_scp_var: rrel_node: _ans;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _elem;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _wait_prog_set2;;
			
			=> nrel_goto: .proc_search_of_object_structure_operator5;;
		*);;

		-> .proc_search_of_object_structure_operator5 
		(*
			<- waitReturn;;
			
			-> rrel_1: rrel_fixed: rrel_scp_var: _wait_prog_set2;;
			
			=> nrel_goto: .proc_search_of_object_structure_operator6;;
		*);;

		//поиск 5тиэлементной конструкции (предмет => элемент)
		-> .proc_search_of_object_structure_operator6
		(*
			<- searchElStr5;;
		
			-> rrel_1: rrel_fixed: rrel_scp_var: _obj;;
			-> rrel_2: rrel_assign: rrel_common: rrel_scp_var: _aarc4;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _elem;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _aarc5;;
	  		-> rrel_5: rrel_fixed: rrel_scp_const: nrel_object_structure;;

			=> nrel_goto: .proc_search_of_object_structure_operator7;;
		*);;

		//добавляем в ответ элемент
		-> .proc_search_of_object_structure_operator7
		(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_node: _ans;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _aarc6;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _elem;;
		
			=> nrel_goto:  .proc_search_of_object_structure_operator8;;
		*);;

		//добавляем в ответ бинарную дугу
		-> .proc_search_of_object_structure_operator8
		(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_node: _ans;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _aarc7;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _aarc4;;
		
			=> nrel_goto:  .proc_search_of_object_structure_operator9;;
		*);;

		//добавляем в ответ дугу, после этого возвращаемся ко множеству элементов
		-> .proc_search_of_object_structure_operator9
		(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_node: _ans;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _aarc8;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _aarc5;;
		
			=> nrel_goto:  .proc_search_of_object_structure_operator2;;
		*);;

		->.proc_search_of_object_structure_operatorreturn 
		(*
			<- return;;
		*);;
	*);;
*);;